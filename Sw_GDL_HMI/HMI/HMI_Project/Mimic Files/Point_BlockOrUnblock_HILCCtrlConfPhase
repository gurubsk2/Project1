ASCII32,13,6,2018,5:31,11,1.1
W,BEGIN,"Point_BlockOrUnblock_HILCCtrlConfPhase","PointBlockOrUnblockHILCCtrlConfPhase"
	TEMPLATE,"",0,0,0,0,0,0,0,0,0,0,0,0
	POSITION,0,0
	SIZE,330,140,330,140,0,0,0,1280,1024,330,140
	BACKCOLOR,191,191,191,0,0,0
	TITLE,0
	STYLE,0,0,0,0,0,0,0,0,0,0,0,0,0,0,1,0,0
	GRID,1,0,8,8,0,0,0,0,0,0
	LAYERS,65535
	RIGHTS,0,1,0.000000,64.000000,1,65535,0,0
	INCLUDED,0,0,0,0,0
	LINK,1,"",""
	LINK,2,"",""
	LINK,3,"",""
	LINK,4,"",""
	LINK,5,"",""
	LINK,6,"",""
	LINK,7,"",""
	LINK,8,"",""
	LINK,9,"",""
	LINK,10,"",""
	BACKBMP,"",0,0
	BACKOBJECT,"",0,0
	BEFORE,"","","",""
	EVENTS,1
	BINOBJ,"Point_BlockOrUnblock_HILCCtrlConfPhase.binary"
W,END
VBA_HOSTPROJECT,BEGIN
	'* *******************************************************************************************
	'* Copyright, ALSTOM Transport Information Solutions, 2008. All Rights Reserved.
	'* The software is to be treated as confidential and it may not be copied, used or disclosed
	'* to others unless authorised in writing by ALSTOM Transport Information Solutions.
	'* *******************************************************************************************
	'* Mimic Name:  Point Block HILC Ctrl Conf Phase
	'* *******************************************************************************************
	'* Purpose:     It manages:
	'*              - the combo box containing the list of names of Points belonging to the track plan
	'*              - the name of the OPC variable associated to the point selected (or entered) by the
	'*                operator in the combobox
	'* *******************************************************************************************
	'* Modification History:
	'* Author:              Danish Draboo
	'* Date:                July '08
	'* Change:              All
	
	'* Author:              Danish Draboo
	'* Date:                Oct '08
	'* Change:              Updated as per RSAD release 5
	
	'* Author:              Nicole FLORIAN
	'* Date:                Nov '08
	'* Change:              Fix anomalies
	
	'* Author:              Nicole FLORIAN
	'* Date:                Dec '08
	'* Change:              CR ALPHA 118291
	'*                      Modify FillComboBox subroutine
	
	'* Author:              Nicole FLORIAN
	'* Date:                Dec '08
	'* Change:              CR ALPHA 119218
	'*                      Remove OPC_HILCConfirmation variable
	
	'* Author:              Olivier TAYEG
	'* Date:                Sept '09
	'* Change:              CR ALPHA 150248
	'*                      Display the switch name and impacted point name
	
	'* Author:              Olivier TAYEG
	'* Date:                Oct '09
	'* Change:              CR ALPHA 153628
	'*                      Do not disable the drop-down list after a selection
	
	'* Author:              Nicole FLORIAN
	'* Date:                May '10
	'* Change:              CR ALPHA 153911
	
	'* Author:              Nicole FLORIAN
	'* Date:                June '10
	'* Change:              CR ALPHA 185097
	
	'* Author:              Nicole FLORIAN
	'* Date:                May '11
	'* Change:              CR ALPHA 233008
	
	'* Author:              Vinay C.R
	'* Date:                Feb '15
	'* Change:              CR 00602340
	'*                      HILC command with Preparation phase and Conformation phase
	
	'* *******************************************************************************************
	'* Ref:             1. REQUIREMENTS SPECIFICATION AND ARCHITECTURE DESCRIPTION
	'*                  2. OPERATIONAL HMI INTERFACE DESCRIPTION
	'* *******************************************************************************************
	
	'* Declarations
	'* *********************
	Option Explicit
	
	'* Constants
	'*******************************************************
	Private Const c_strServerSeparator As String = ":"
	Private Const c_strSeparator As String = "\n"
	Private Const c_strNoOtherPoint As String = "NA"
	
	Private Const c_strPointEndNode As String = "PointEnd"
	Private Const c_strID As String = "ID"
	Private Const c_strName As String = "Name"
	Private Const c_strArea As String = "Area"
	Private Const c_strSwitchName As String = "Switch"
	
	Private Const c_strDevicesListTag As String = "IconisHMI.PointEndsList.Information.Value"
	Private Const c_strImpactedPointEndsTag As String = ".ImpactedPointEnds.Value"
	
	Private Const c_strImpactedPointsTag As String = ".ImpactedPoints%"
	Private Const c_strSwitchNameTag As String = ".SwitchName%"
	
	'* OPC variables
	'*******************************************************
	Dim WithEvents m_OPC_DevicesNamesList As Variable
	'* List of the impacted points, used to obtain the name of the associated switch
	Dim WithEvents m_OPC_ImpactedPointEnds As Variable
	
	'* Local variable containing the name of the switch
	Dim m_OPClocal_SwitchName As Variable
	'* Local variable containing the list of the impacted points
	Dim m_OPClocal_ImpactedPointsList As Variable
	
	'* SubRoutine: Mimic_Open
	'*      Adds the OPC variable to the collection of all permanently subscribed variables
	'* ******************************************************************************************
	Private Sub Mimic_Open()
	On Error GoTo ErrorHandler
	    Call CBTrace(CBTRACE_VBA, ThisMimic.Name, "Mimic_Open", "Begin Subroutine")
	   
	    Dim strClusterName As String
	    
	    '* Disable the combo box
	    ComboBoxDeviceNames.Enabled = False
	    
	    '* Add a variable containing the OPC variable name of the device
	    '* selected by the operator in the combo box
	    Variables.Add ThisMimic.Branch & ".ConfPhaseDeviceOPCVarName%", fvVariableTypeText
	    
	    '* Add a variable containing the OPC variable names of each device
	    '* belonging to the track plan
	    Variables.Add ThisMimic.Branch & ".ListOfOPCVarNames%", fvVariableTypeText
	   
	    '* Add variable containing the list of devices to the list of variables
	    strClusterName = Left(ThisMimic.Branch, InStr(1, ThisMimic.Branch, c_strServerSeparator, vbTextCompare))
	    Set m_OPC_DevicesNamesList = Variables.Add(strClusterName & c_strDevicesListTag, fvVariableTypeText)
	    If ((m_OPC_DevicesNamesList.Status = fvVariableStatusGood) And (m_OPC_DevicesNamesList.EnableEvents = True)) Then
	        m_OPC_DevicesNamesList_ValueChange
	    Else
	        m_OPC_DevicesNamesList.EnableEvents = True
	    End If
	    
	    '* Add and reset the variable used for the switch name; The default value is a string ""
	    Set m_OPClocal_SwitchName = Variables.Add(ThisMimic.Branch & c_strSwitchNameTag, fvVariableTypeText)
	    m_OPClocal_SwitchName.Value = ""
	    
	    '* Add and reset the variable used for the impacted point ends; The default value is a string ""
	    Set m_OPClocal_ImpactedPointsList = Variables.Add(ThisMimic.Branch & c_strImpactedPointsTag, fvVariableTypeText)
	    m_OPClocal_ImpactedPointsList.Value = ""
	
	Exit Sub
	ErrorHandler:
	    Call CBTrace(CBTRACEF_ALWAYS, ThisMimic.Name, "Mimic_Open", Err.Description)
	End Sub
	
	'* Function: Mimic_BeforeClose
	'*      Unsubscribe the OPC variables previously subscribed
	'* ******************************************************************************************
	Private Function Mimic_BeforeClose() As Boolean
	On Error GoTo ErrorHandler
	    Call CBTrace(CBTRACE_VBA, ThisMimic.Name, "Mimic_BeforeClose", "Begin Function")
	    
	    Variables.Remove (ThisMimic.Branch & ".ConfPhaseDeviceOPCVarName%")
	    Variables.Remove (ThisMimic.Branch & ".ListOfOPCVarNames%")
	       
	    If Not (m_OPC_DevicesNamesList Is Nothing) Then
	        Variables.Remove (m_OPC_DevicesNamesList.Name)
	        Set m_OPC_DevicesNamesList = Nothing
	    End If
	    
	    If Not (m_OPClocal_SwitchName Is Nothing) Then
	        Variables.Remove (m_OPClocal_SwitchName.Name)
	        Set m_OPClocal_SwitchName = Nothing
	    End If
	    
	    If Not (m_OPClocal_ImpactedPointsList Is Nothing) Then
	        Variables.Remove (m_OPClocal_ImpactedPointsList.Name)
	        Set m_OPClocal_ImpactedPointsList = Nothing
	    End If
	    
	    If Not (m_OPC_ImpactedPointEnds Is Nothing) Then
	        Variables.Remove (m_OPC_ImpactedPointEnds.Name)
	        Set m_OPC_ImpactedPointEnds = Nothing
	    End If
	    
	Exit Function
	ErrorHandler:
	    Call CBTrace(CBTRACEF_ALWAYS, ThisMimic.Name, "Mimic_BeforeClose", Err.Description)
	End Function
	
	'* SubRoutine: m_OPC_DevicesNamesList_ValueChange
	'*      Manages filling of the Combo box
	'* ******************************************************************************************
	Private Sub m_OPC_DevicesNamesList_ValueChange()
	On Error GoTo ErrorHandler
	    Call CBTrace(CBTRACE_VBA, ThisMimic.Name, "m_OPC_DevicesNamesList_ValueChange", "Begin Subroutine")
	    
	    '* Check the status and the Quality of the Variable
	    If Check_OPC_Variable(m_OPC_DevicesNamesList) Then
	      '* Fill the combo box
	        FillComboBox
	    End If
	
	Exit Sub
	ErrorHandler:
	    Call CBTrace(CBTRACEF_ALWAYS, ThisMimic.Name, "m_OPC_DevicesNamesList_ValueChange", Err.Description)
	End Sub
	
	'* SubRoutine: ComboBoxDeviceNames_Change
	'*      Manages getting the value selected in the combo box
	'* ******************************************************************************************
	Private Sub ComboBoxDeviceNames_Change()
	On Error GoTo ErrorHandler
	    Call CBTrace(CBTRACE_VBA, ThisMimic.Name, "ComboBoxDeviceNames_Change", "Begin Subroutine")
	    
	    '* Each time the selected devices changes, the fields "Switch name" and "Impacted Point List" are reset
	    '* The OPC subscription are repeated here because of CR ALPHA 150228
	
	    '* Add and reset the variable used for the switch name; The default value is a string ""
	    Set m_OPClocal_SwitchName = Variables.Add(ThisMimic.Branch & c_strSwitchNameTag, fvVariableTypeText)
	    m_OPClocal_SwitchName.Value = ""
	
	    '* Add and reset the variable used for the impacted point ends; The default value is a string ""
	    Set m_OPClocal_ImpactedPointsList = Variables.Add(ThisMimic.Branch & c_strImpactedPointsTag, fvVariableTypeText)
	    m_OPClocal_ImpactedPointsList.Value = ""
	    
	    If (ComboBoxDeviceNames.ListIndex > -1) Then
	        Dim OPCVarNamesArray() As String
	        Dim strClusterName As String
	    
	        OPCVarNamesArray = Split(Variables.Item(ThisMimic.Branch & ".ListOfOPCVarNames%").Value, c_strSeparator)
	        If (UBound(OPCVarNamesArray) > 0) Then
	            '* Memorize the OPC variable name of the selected device
	            Variables.Item(ThisMimic.Branch & ".ConfPhaseDeviceOPCVarName%").Value = OPCVarNamesArray(ComboBoxDeviceNames.ListIndex)
	            
	            '* Enable the OK button of the parent mimic
	            Variables.Item(ThisMimic.Branch & ".ButtonOKDisabled%").Value = 0
	            
	            '* For the point selected, get the ImpactedPointEnds information
	            '* Adding a OPC variable ImpactedPointEnds to the list of variables
	            strClusterName = Left(ThisMimic.Branch, InStr(1, ThisMimic.Branch, c_strServerSeparator, vbTextCompare))
	            Set m_OPC_ImpactedPointEnds = Variables.Add(strClusterName & OPCVarNamesArray(ComboBoxDeviceNames.ListIndex) & c_strImpactedPointEndsTag, fvVariableTypeText)
	            If (m_OPC_ImpactedPointEnds.EnableEvents = True) Then
	                m_OPC_ImpactedPointEnds_ValueChange
	            Else
	                m_OPC_ImpactedPointEnds.EnableEvents = True
	            End If
	
	        End If
	    End If
	
	Exit Sub
	ErrorHandler:
	    Call CBTrace(CBTRACEF_ALWAYS, ThisMimic.Name, "ComboBoxDeviceNames_Change", Err.Description)
	End Sub
	
	'*  SubRoutine: FillComboBox
	'*    Read the xml string containing the names of the devices belonging to the track plan
	'*    Fill the combo box
	'*    Memorizes the list of OPC variable names of each device belonging to the track plan
	'* ******************************************************************************************
	Private Sub FillComboBox()
	On Error GoTo ErrorHandler
	    Call CBTrace(CBTRACE_VBA, ThisMimic.Name, "FillComboBox", "Begin Subroutine")
	    
	    Dim strXML As String
	    
	    '* Clear the combo box
	    ComboBoxDeviceNames.Clear
	    '* Reset the property Value of the combo box
	    ComboBoxDeviceNames.Value = ""
	    '* Disable the combo box
	    ComboBoxDeviceNames.Enabled = False
	    
	    strXML = m_OPC_DevicesNamesList.Value
	    If (strXML <> "") Then
	        Dim oDoc As DOMDocument
	  
	        Set oDoc = New DOMDocument
	        '* Open the XMl file
	        If (oDoc.loadXML(strXML)) Then
	            Dim FirstNodeLevel As IXMLDOMNodeList
	           
	            Set FirstNodeLevel = oDoc.documentElement.getElementsByTagName(c_strPointEndNode)
	            If (FirstNodeLevel.length <> 0) Then
	                Dim oNodeClass As IXMLDOMNode
	                Dim oAttrDevice As IXMLDOMAttribute
	                Dim strName As String
	                Dim strVariableName As String
	                Dim strAreaName As String
	                    
	                For Each oNodeClass In FirstNodeLevel
	                    strName = ""
	                    strVariableName = ""
	                    strAreaName = ""
	                    For Each oAttrDevice In oNodeClass.Attributes
	                        If (StrComp(oAttrDevice.nodeName, c_strName, vbTextCompare) = 0) Then
	                            strName = oAttrDevice.nodeValue
	                        ElseIf (StrComp(oAttrDevice.nodeName, c_strID, vbTextCompare) = 0) Then
	                            strVariableName = oAttrDevice.nodeValue
	                        ElseIf (StrComp(oAttrDevice.nodeName, c_strArea, vbTextCompare) = 0) Then
	                            strAreaName = oAttrDevice.nodeValue
	                        End If
	                    Next
	                    
	                    If ((strName <> "") And (strVariableName <> "")) And InStr(1, ThisMimic.Branch, Split(strAreaName, "_")(1), vbTextCompare) > 0 Then
	                      '  If (StrComp(strAreaName, Variables.Item(ThisMimic.Branch & ".PrepPhaseDeviceAreaName%").Value, vbTextCompare) = 0) Then
	                        ComboBoxDeviceNames.AddItem (strName)
	                            Variables.Item(ThisMimic.Branch & ".ListOfOPCVarNames%").Value = Variables.Item(ThisMimic.Branch & ".ListOfOPCVarNames%").Value & _
	                                                                                             strVariableName & c_strSeparator
	                       ' End If
	                    End If
	                Next
	                If (ComboBoxDeviceNames.ListCount > 0) Then
	                    '* Enable the combo box (if it exists at least one item)
	                    ComboBoxDeviceNames.Enabled = True
	                End If
	            End If
	            Set FirstNodeLevel = Nothing
	        End If
	        Set oDoc = Nothing
	    End If
	    
	Exit Sub
	ErrorHandler:
	    Call CBTrace(CBTRACEF_ALWAYS, ThisMimic.Name, "FillComboBox", Err.Description)
	End Sub
	
	'-------------------------------------------------------------------------------
	' Name:         m_OPC_ImpactedPointEnds_ValueChange
	' Input:        none
	' Output:       none
	' Description:  Extract the name of the switch and of the other point from the XML string
	'-------------------------------------------------------------------------------
	Private Sub m_OPC_ImpactedPointEnds_ValueChange()
	On Error GoTo ErrorHandler
	    Call CBTrace(CBTRACE_VBA, ThisMimic.Name, "m_OPC_ImpactedPointEnds_ValueChange", "Begin Subroutine")
	    
	    Dim strCommand As String
	    
	    '* Check the status and the quality of the variable
	    If Check_OPC_Variable(m_OPC_ImpactedPointEnds) Then
	        Dim oDoc As DOMDocument
	        Dim oListPointEnds As IXMLDOMNodeList
	        Dim oPointEnd As IXMLDOMNode
	        Dim oAttrDest As IXMLDOMAttribute
	        Dim bFound As Boolean
	        
	        '* This is the name of this point's switch
	        Dim strSwitchName As String
	    
	        Set oDoc = New DOMDocument
	        If oDoc.loadXML(m_OPC_ImpactedPointEnds.Value) Then
	            '* Read the XML format string to get the name of the point's switch
	            For Each oAttrDest In oDoc.documentElement.Attributes
	                If (StrComp(oAttrDest.nodeName, c_strSwitchName, vbTextCompare) = 0) Then
	                    '* Switch name
	                    strSwitchName = oAttrDest.nodeValue
	                End If
	            Next oAttrDest
	            
	            '* Store the switch name into the animation variable
	            m_OPClocal_SwitchName.Value = strSwitchName
	           
	            '* In the list of the impacted points, find the one from the same switch
	            Set oListPointEnds = oDoc.documentElement.getElementsByTagName(c_strPointEndNode)
	            bFound = False
	            For Each oPointEnd In oListPointEnds
	                Dim strOtherPointSwitchName As String
	                
	                For Each oAttrDest In oPointEnd.Attributes
	                    Dim strName As String
	                    Select Case oAttrDest.nodeName
	                        Case c_strSwitchName
	                            '* Switch name
	                            strOtherPointSwitchName = oAttrDest.nodeValue
	                        Case c_strName
	                            strName = oAttrDest.nodeValue
	                    End Select
	                Next oAttrDest
	                '* Is the point in the same switch?
	                If (strOtherPointSwitchName = strSwitchName) Then
	                    m_OPClocal_ImpactedPointsList.Value = strName
	                    bFound = True
	                End If
	            Next oPointEnd
	            If (bFound = False) Then
	                '* No other point in the switch : display the character '-'
	                m_OPClocal_ImpactedPointsList.Value = c_strNoOtherPoint
	            End If
	            
	            Set oListPointEnds = Nothing
	            Set oDoc = Nothing
	        End If
	    End If
	
	Exit Sub
	ErrorHandler:
	    Call CBTrace(CBTRACEF_ALWAYS, ThisMimic.Name, "m_OPC_ImpactedPointEnds_ValueChange", Err.Description)
	End Sub
	
	
	
	
	
	
	
	
	
	
VBA_HOSTPROJECT,END
VBA_REFERENCES,BEGIN
	"stdole", 2, 0, "{00020430-0000-0000-C000-000000000046}"
	"MSForms", 2, 0, "{0D452EE1-E08F-101A-852E-02608C4D0BB4}"
	"MSXML2", 3, 0, "{F5078F18-C551-11D3-89B9-0000F81FE221}"
VBA_REFERENCES,END
FONTS,BEGIN
	FONT,1,-13,0,700,0,0,"Tahoma",0,0
	FONT,2,-13,0,700,0,0,"Arial",0,0
	FONT,3,-13,0,400,0,0,"Tahoma",0,0
FONTS,END
COLORS,BEGIN
	COLOR,1,255,255,255,0,0,0
	COLOR,2,0,0,0,0,0,0
	COLOR,3,0,0,0,0,0,1
	COLOR,4,191,191,191,0,0,0
COLORS,END
O,BEGIN,R,"Shape1"
	B,260,100,5,7,260,100,65535,0,6400,0,1,0,0
	ST,0,1,COLOR,1,COLOR,1,1,3,COLOR,3,3,COLOR,3,0,COLOR,2,0,COLOR,2
O,END
O,BEGIN,T,"Text1"
	B,12,0,109,16,12,16,65535,0,6400,1,1,0,0
	ST,0,1,COLOR,2,COLOR,2,0,0,COLOR,4,0,COLOR,4,0,COLOR,2,0,COLOR,2
	PP,0,12,0,0,0,FONT,1,"Confirmation",36,FONT,1,"Confirmation",36
	MULTILANG,BEGIN
		LANG_DEFAULT,1033
		LANG,BEGIN,1033
			ROLE_DEFAULT,-1
			ROLE,-1,"Confirmaci¢n",FONT,2,36
		LANG,END
		LANG,BEGIN,13322
			ROLE_DEFAULT,0
			ROLE,0,"Confirmaci¢n",FONT,3,36
		LANG,END
	MULTILANG,END
O,END
O,BEGIN,T,"Text2"
	B,20,34,69,50,20,50,65535,0,6400,0,1,0,0
	ST,0,1,COLOR,2,COLOR,2,0,0,COLOR,3,0,COLOR,3,0,COLOR,2,0,COLOR,2
	PP,0,20,34,0,0,FONT,1,"Point",37,FONT,1,"Point",37
	MULTILANG,BEGIN
		LANG_DEFAULT,1033
		LANG,BEGIN,1033
			ROLE_DEFAULT,-1
			ROLE,-1,"Aguja",FONT,2,37
		LANG,END
		LANG,BEGIN,13322
			ROLE_DEFAULT,0
			ROLE,0,"Aguja",FONT,1,37
		LANG,END
	MULTILANG,END
O,END
O,BEGIN,OCX,"ComboBoxDeviceNames"
	B,25,59,176,79,10,60,65535,0,6400,0,1,1,1
	ST,5,1,COLOR,3,COLOR,3,1,0,COLOR,3,0,COLOR,3,0,COLOR,2,0,COLOR,2
O,END
O,BEGIN,T,"Text3"
	B,16,127,70,143,16,143,65535,0,6400,0,1,0,0
	ST,0,1,COLOR,2,COLOR,2,0,0,COLOR,3,0,COLOR,3,0,COLOR,2,0,COLOR,2
	PP,0,16,127,0,0,FONT,2,"Aguja",37,FONT,2,"Aguja",37
	MULTILANG,BEGIN
		LANG_DEFAULT,1033
		LANG,BEGIN,1033
			ROLE_DEFAULT,-1
			ROLE,-1,"Estado",FONT,2,37
		LANG,END
		LANG,BEGIN,13322
			ROLE_DEFAULT,0
			ROLE,0,"Aguja",FONT,1,37
		LANG,END
	MULTILANG,END
O,END
